apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 19
        // Required to bypass runtime Camera permissions
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 19
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        versionCode 31
        versionName "$ftc_version.0-lite"

        ndk {
            abiFilters "armeabi-v7a"
        }

        consumerProguardFiles 'proguard-rules.pro'
    }

    aaptOptions {
        noCompress "tflite"
    }
}

group = 'com.github.modular-ftc'
version = android.defaultConfig.versionName
project.archivesBaseName = 'robotcontroller-repackaged'

dependencies {
    // Original dependencies
    implementation "org.first.ftc:inspection:$ftc_version"
    implementation "org.first.ftc:hardware:$ftc_version"
    implementation "org.first.ftc:analytics:$ftc_version"
    implementation "org.first.ftc:wireless-p2p:$ftc_version"

    // Repackaged dependencies
    implementation "com.github.modular-ftc:robotcore-repackaged:$ftc_version.1-lite"
    implementation "com.github.modular-ftc:ftc-common-repackaged:$ftc_version.1-lite"
    implementation "com.github.modular-ftc:vuforia-repackaged:$ftc_version.1-lite"

    // Android dependencies
    implementation 'com.android.support:support-annotations:28.0.0'
}

//For JitPack

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}